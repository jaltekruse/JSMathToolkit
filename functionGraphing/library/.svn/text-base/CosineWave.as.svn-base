/**
 * <p><code>CosWave</code> is a helper class for plotting sine waves of the form</p>
 * 
 * <p>a*cos(bx) + c</p>
 *
 * <p>This function takes parameters <code>a</code>, <code>b</code>, and <code>c</code>, with <code>a = 1</code>, <code>b = 1</code>, and <code>c = 0</code> as
 * defaults.</p>
 *
 * @author Jim Armstrong
 * @version 1.0
 *
 * @keyword function sine cosine
 * 
 */

package com.explorelearning.hilo.display.graphing.functions.library
{
	 import com.explorelearning.hilo.display.graphing.functions.FunctionTypeEnum;
	 import com.explorelearning.hilo.display.graphing.functions.QuadrantEnum;
	 
  import flash.utils.getDefinitionByName;
  import flash.utils.getQualifiedClassName;
	 
/**
 * <code>CosineWave</code> Create a new <code>CosineWave</code> instance.
 * 
 * @return Nothing
 * 
 * @since 1.0
 */
	 public class CosineWave extends BaseFunction
	 { 
	   // parameters
	   protected var _a:Number;
	   protected var _b:Number;
	   protected var _c:Number;
	   
	   public function CosineWave():void
	   {
	     super();
	     
	     _type = FunctionTypeEnum.TRANSCENDENTAL;
	     
	     _a = 1;
	     _b = 1;
	     _c = 0;
	   }
    
/**
 * <code>toString()</code> Returns a generic string representation of the <code>CosineWave</code>.
 * 
 * @return String. a.cos(bx) + c, where '.' is the platform multiply symbol.
 * 
 * @since 1.0
 */
    override public function toString():String { return "a" + MULTIPLY + "cos(bx) + c"; }
    
/**
 * <code>[set] params</code> Assign the <code>CosineWave</code> parameters.
 * 
 * @param pString:String - Parameter list.  <code>CosineWave</code> takes parameters <code>a</code>, <code>b</code>, and <code>c</code>, with <code>a = 1</code>, 
 * <code>b = 1</code>, and <code>c = 0</code> as defaults.
 * 
 * @return Number.  Function value or <code>NaN</code> if the function is not defined at the specified domain value.
 * 
 * @since 1.0
 */
    override public function set params(pParams:String):void
    {
      if( pParams == "" )
      {
        return;
      }
      
      var param:Array = pParams.split(",");
      for( var i:uint=0; i<param.length; ++i )
      {
        var myParams:Array = param[i].split(":");
        var name:String    = myParams[0].toLowerCase();
        var num:Number     = myParams[1];
        
        switch( name)
        {
          case "a":
            _a = isNaN(num) ? _a : num;
          break;
          
          case "b":
            _b = isNaN(num) ? _b : num;
          break;
          
          case "c":
            _c = isNaN(num) ? _c : num;
          break;
        }
      }
    }
    
/**
 * <code>eval</code> Evaluate the <code>CosineWave</code> function.
 * 
 * @param pX:Number x-coordinate .
 * 
 * @return Number.  Function value at the specified x-coordinate, given the current parameter settings.
 * 
 * @since 1.0
 */
    override public function eval(pX:Number):Number
    {   
      return _a*Math.cos(_b*pX) + _c;
    }
    
/**
 * <code>derivative</code> Evaluate the <code>CosineWave</code> first derivative.
 * 
 * @param pX:Number x-coordinate .
 * 
 * @return Number.  First derivative value at the specified x-coordinate, given the current parameter settings.
 * 
 * @since 1.0
 */
    override public function derivative(pX:Number):Number
    {
      return -_a*_b*Math.sin(pX);
    }
    
 /**
 * <code>getCartesianMinima</code> Return x-coordinates of all local minima in an interval.
 * 
 * @param pA:Number Left endpoint of interval [a,b].
 * @param pB:Number Right endponig of interval [a,b].
 * 
 * @return Array.  x-cooridinates of all local minima in [a,b].  This allows the <code>CosineWave</code> function to be quickly plotted with a quadratic Bezier
 * approximation. Accuracy is enforced by enforcing the approximation to exactly match the original function at stationary points.
 * 
 * @since 1.0
 */
    override public function getCartesianMinima(pA:Number, pB:Number):Array
    {
      if( _a == 0 )
      {
        return [];
      }
      
      // for positive a, minima are at odd multiples of pi/b, so get next odd multiple in the open interval
      var value:Number = Math.PI/_b;
      var mult:Number  = Math.round(pA/value);
      
      if( _a > 0 )
      {
        if( mult % 2 == 0 )
        {
           mult += 1.0;
        }
      }
      else
      {
        if( mult % 2 != 0 )
        {
           mult += 1.0;
        }
      }
      
      var twoV:Number = 2*value;
      var next:Number = mult*value;
      if( next >= pB )
      {
        return [];
      }
      
      var coords:Array = next <= pA ? [] : [next];
      var i:uint       = coords.length == 0 ? 0 : 1;
      next            += twoV;
      
      while( next < pB )
      {
        coords[i++] = next;
        next       += twoV;
      }
      
      return coords;
    }
    
 /**
 * <code>getCartesianMaxima</code> Return x-coordinates of all local maxima in an interval.
 * 
 * @param pA:Number Left endpoint of interval [a,b].
 * @param pB:Number Right endponig of interval [a,b].
 * 
 * @return Array.  x-cooridinates of all local maxima in [a,b].  This allows the <code>CosineWave</code> function to be quickly plotted with a quadratic Bezier
 * approximation. Accuracy is enforced by enforcing the approximation to exactly match the original function at stationary points.
 * 
 * @since 1.0
 */
    override public function getCartesianMaxima(pA:Number, pB:Number):Array
    {
      if( _a == 0 )
      {
        return [];
      }
      
      // for positive a, maxima are at even multiples of pi/b, so get next even multiple in the open interval
      var value:Number = Math.PI/_b;
      var mult:Number  = Math.round(pA/value);
      
      if( _a > 0 )
      {
        if( mult % 2 != 0 )
        {
          mult += 1.0;
        }
      }
      else
      {
        if( mult % 2 == 0 )
        {
          mult += 1.0;
        }
      }
      
      var twoV:Number = 2*value;
      var next:Number = mult*value;
      if( next >= pB )
      {
        return [];
      }
      
      var coords:Array = next <= pA ? [] : [next];
      var i:uint       = coords.length == 0 ? 0 : 1;
      next            += twoV;
      
      while( next < pB )
      {
        coords[i++] = next;
        next       += twoV;
      }
      
      return coords;
    }
    
    // bounds are used for clipping, so return them accurately if it's easy; if not, don't worry about it
    override protected function computeBounds():void
    {
      _top    = _c + _a;
	     _bottom = _c - _a;
    }
    
/**
 * <code>formatFunction</code> Format the functional representation given a collection of parameter values.
 * 
 * @param pParams:Array <code>String</code> values for each of the parameters, a, b, and c.
 * @param pX:Number Reserved for future use
 * @default NaN
 * 
 * @return String.  Representation of the <code>CosineWave</code> function given the input parameters, in simplified form.
 * 
 * @since 1.0
 */
    override public function formatFunction(pParams:Array, pX:Number=NaN):String
    {
      if( pParams.length == 0 )
      {
        return "0";
      }
      
      var a:String = pParams[0];
      var b:String = pParams.length == 1 ? "0" : pParams[1];
      var c:String = pParams.length == 2 ? "0" : pParams[2];
      
      if( b == "" || b == " " || b == "0" )
      {
        return twoParam("", "", c);
      }
      else if( b == "1" )
      {
        return twoParam(a, "cos(x)", c);
      }
      else if( b == "-1" )
      {
        return twoParam(a, "cos(-x)", c);
      }
      else if( b.charAt(0) == "-" )
      {
        return twoParam(a, "cos("+MINUS+b.substring(1,b.length)+")", c);
      }
      else
      {
        return twoParam(a, "cos("+b+"x)", c);
      }
     
    }
    
    private function twoParam(pA:String, pB:String, pC:String):String
    {
      if( pA == "" || pA == " " || pA == "0" )
      {
        return oneParam("", pC);
      }
      else if( pA == "1" )
      {
        return oneParam(pB, pC);
      }
      else if( pA == "-1" )
      {
        return oneParam(MINUS+pA, pC);
      }
      else if( pA.charAt(0) == "-" )
      {
        return oneParam(MINUS+pA.substring(1,pA.length)+MULTIPLY+pB, pC);
      }
      else
      {
        return oneParam(pA+MULTIPLY+pB, pC);
      }
    }
    
    private function oneParam(pA:String, pC:String):String
    {
      if( pC == "" || pC == " " || pC == "0" )
      {
        return pA == "" ? "0" : pA;
      }
      else if( pC.charAt(0) == "-" )
      {
        return pA == "" ? MINUS + pC.substring(1,pC.length) : pA + " " + MINUS + " " + pC.substring(1,pC.length);
      }
      else
      {
        return pA == "" ? pC : pA + " " + PLUS + " " + pC;
      }
    }
    
/**
 * <code>formatDerivative</code> Format the first derivative functional representation given a collection of parameter values.
 * 
 * @param pParams:Array <code>String</code> values for each of the parameters, a, b, and c.
 * @param pX:Number Reserved for future use
 * @default NaN
 * 
 * @return String.  Representation of the <code>CosineWave</code> first derivative given the input parameters, in simplified form.
 * 
 * @since 1.0
 */
    override public function formatDerivative(pParams:Array, pX:Number=NaN):String
    {
      if( pParams.length == 0 )
      {
        return "0";
      }
      
      var a:String = pParams[0];
      var b:String = pParams.length == 1 ? "0" : pParams[1];
      var c:String = pParams.length == 2 ? "0" : pParams[2];
      
      if( b == "" || b == " " || b == "0" )
      {
        return twoParamDeriv(a, "", c);
      }
      else if( b == "1" )
      {
        return twoParamDeriv(a, "sin(x)", c);
      }
      else if( b == "-1" )
      {
        return twoParamDeriv(a, "sin(-x)", c);
      }
      else if( b.charAt(0) == "-" )
      {
        return twoParamDeriv(a, "sin("+MINUS+b.substring(1,b.length)+")", c);
      }
      else
      {
        return twoParamDeriv(a, "sin("+b+"x)", c);
      }
    }
    
    private function twoParamDeriv(pA:String, pB:String, pC:String):String
    {
      if( pA == "" || pA == " " || pA == "0" )
      {
        return oneParam("", pC);
      }
      else if( pA == "1" )
      {
        return oneParam(MINUS+pB, pC);
      }
      else if( pA == "-1" )
      {
        return oneParam(pB, pC);
      }
      else if( pA.charAt(0) == "-" )
      {
        return oneParam(pA.substring(1,pA.length)+MULTIPLY+pB, pC);
      }
      else
      {
        return oneParam(MINUS+pA+MULTIPLY+pB, pC);
      }
    }
	 }
}